apply plugin: 'maven-publish'

def baseArtifactId = project.baseId
def javaBaseName = "_GROUP_edu_wpi_first_${project.baseId}_ID_${project.baseId}-java_CLS"

def outputsFolder = file("$project.buildDir/outputs")

task javadoc(type: Javadoc, overwrite: true) {
    source = sourceSets.main.allJava
    classpath = configurations.compile

    javadoc.options.links("http://docs.oracle.com/javase/8/docs/api/")
    options.addStringOption "tag", "pre:a:Pre-Condition"
    options.addStringOption('Xdoclint:accessibility,syntax,reference,html', '-quiet')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
     repositories {
       maven {
           url maven_publishing_path
       }
    }

    publications {

        snobot_sim_java(MavenPublication) {
            groupId 'com.snobot.simulator'
            artifactId "${baseArtifactId}"
            version maven_version

            artifact jar
            artifact javadocJar
            artifact sourcesJar
            //artifact combineNativeLibrariesIntoOne
        }
    }
}
