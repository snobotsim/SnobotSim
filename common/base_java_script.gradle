
import org.gradle.api.tasks.testing.logging.TestLogEvent

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'com.github.spotbugs'
apply plugin: 'eclipse'

repositories {
    mavenCentral()
    mavenLocal()

    maven { url getWpiRepo() }
    maven { url getNavxRepo() }
    maven { url getCtreRepo() }
    maven { url getRevRepo() }
    maven { url getSnobotSimRepo() }
}

jacoco {
    toolVersion = "0.8.4"
}
jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events TestLogEvent.FAILED,
               TestLogEvent.SKIPPED
//               TestLogEvent.STANDARD_OUT
        exceptionFormat "full"
    }
}

compileJava {
    sourceCompatibility = 11
    targetCompatibility = 11
}

task run(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath

    main = "com.snobot.simulator.Main"
}

/////////////////////////////////////////////////////////////////////////////////
// Style/Formatting stuff
checkstyle {
    toolVersion = "8.21"
    configDir = file("${project.rootDir}/styleguide")
    config = resources.text.fromFile(new File(configDir, "checkstyle.xml"))
}

pmd {
    toolVersion = '6.15.0'
    consoleOutput  = true
    reportsDir = file("${project.buildDir}/reports/pmd")
    ruleSetFiles = files(file("$rootDir/styleguide/pmd-ruleset.xml"))
    ruleSets = []
}

spotbugs {
  toolVersion = '3.1.8'
  excludeFilter = file("$rootDir/styleguide/findbugs_suppressions.xml")
  effort = "max"
}

tasks.withType(SpotBugsTask) {
    reports {
        xml.enabled false
        html.enabled true
    }
}
/////////////////////////////////////////////////////////////////////////////////

apply from: "${rootDir}/common/base_java_publish.gradle"
