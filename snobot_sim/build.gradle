import edu.wpi.first.nativeutils.NativeUtils
import org.gradle.api.file.FileCollection
import org.gradle.internal.os.OperatingSystem

repositories {
    mavenCentral()
    maven {
        url "http://first.wpi.edu/FRC/roborio/maven/release/"
    }
}

apply plugin: 'cpp'
apply plugin: 'visual-studio'
apply plugin: 'edu.wpi.first.NativeUtils'

apply from: '../config.gradle'

ext.addSnobotSimToLinker = { binary->
    binary.lib project: ':snobot_sim', library: 'snobotSimCpp', linkage: 'shared'
}

task packageNativeFilesInJar(type: Jar) {

    destinationDir = project.buildDir
    classifier = "snobotSim-native-" + org.gradle.internal.os.OperatingSystem.current().getFamilyName();

    project.model {
        binaries {
            withType(SharedLibraryBinarySpec) { binary ->
                if (binary.component.name == 'snobotSimCpp') {
                    dependsOn binary.buildTask
                    from(binary.sharedLibraryFile) {
                        into NativeUtils.getPlatformPath(binary)
                    }
                }
            }
        }
    }
}
build.dependsOn packageNativeFilesInJar


model {
    dependencyConfigs {
        wpiutil(DependencyConfig) {
            groupId = 'edu.wpi.first.wpiutil'
            artifactId = 'wpiutil-cpp'
            headerClassifier = 'headers'
            ext = 'zip'
            version = getWpiUtilVersion()
            sharedConfigs = [ snobotSimCpp: [] ]
        }
        halsim(DependencyConfig) {
            groupId = 'edu.wpi.first.hal'
            artifactId = 'hal'
            headerClassifier = 'headers'
            ext = 'zip'
            version = allwpilibVersion()
            sharedConfigs = [ snobotSimCpp: [] ]
        }
        adxfamily(DependencyConfig) {
            groupId = 'edu.wpi.first.halsim'
            artifactId = 'halsim-adx_gyro_accelerometer'
            headerClassifier = 'headers'
            ext = 'zip'
            version = allwpilibVersion()
            sharedConfigs = [ snobotSimCpp: [] ]
        }
    }
    components {
        snobotSimCpp(NativeLibrarySpec) {
            baseName = 'snobotSimCpp'
            
            apply from: "../create_version_file.gradle"
            createCppVersion("src/main/native/cpp/SnobotSim", "SnobotSim", "SnobotSimHalVersion", getVersionName())
            
            sources {
                cpp {
                    source {
                        srcDirs = ['src/main/native/cpp']
                        includes = ['**/*.cpp']
                    }
                    exportedHeaders {
                        srcDirs = ["src/main/native/include"]
                    }
                    lib project: ':sim_extension_navx', library: 'navx_simulator', linkage: 'shared'
                }
            }
        }

    }
    binaries {
        withType(StaticLibraryBinarySpec) {
            it.buildable = false
        }
    }
}

apply from: 'publish.gradle'
