import edu.wpi.first.nativeutils.NativeUtils
import org.gradle.api.file.FileCollection
import org.gradle.internal.os.OperatingSystem


repositories {
    mavenCentral()
    maven {
        url getJaci3rdPartyMirror()
    }
}

apply plugin: 'cpp'
apply plugin: 'visual-studio'
apply plugin: 'edu.wpi.first.NativeUtils'
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'findbugs'

apply from: '../config.gradle'

sourceSets {
    main {
        java {
            srcDirs = ["java/src"]
        }
    }
    test {
        java {
            srcDirs = ["java/unit_tests"]
            exclude '**/simulator_components/ctre/**'
            exclude '**/com/ctre/**'
        }
    }
}
test {
    ignoreFailures = !OperatingSystem.current().isWindows()
}

task packageNativeFilesInJar(type: Jar) {
    
    dependsOn jar
    
    destinationDir = project.buildDir
    classifier = "native-" + org.gradle.internal.os.OperatingSystem.current().getFamilyName();

    project.model {
        binaries {
            withType(SharedLibraryBinarySpec) { binary ->
                if (binary.component.name == 'snobotSimJniShared') {
                    from(binary.sharedLibraryFile) {
                        into NativeUtils.getPlatformPath(binary)
                    }
                }
            }
        }
    }
}

apply from: "$rootDir/captureWpiLibraries.gradle"

task combineNativeLibrariesIntoOne(type: Jar) {

    dependsOn packageNativeFilesInJar
    dependsOn captureWpiLibraries

    def os_name = org.gradle.internal.os.OperatingSystem.current().getFamilyName();

    classifier = "uber_native-" + os_name;
    
    packageNativeFilesInJar.outputs.files.each { 
        from(zipTree(it))
    }
    
    captureWpiLibraries.outputs.files.each {
        from(zipTree(it))
    }

    from(zipTree('../snobot_sim/build/snobotSim-native-' + os_name + ".jar"))
    
    def navx_path = '../sim_extension_navx/build/libs/navx_simulator/shared/'
    from(navx_path + 'navx_simulator.dll') { 
        into "windows/x86-64"
    }
    from(navx_path + 'libnavx_simulator.so') { 
        into "linux/x86-64"
    }
}
build.dependsOn packageNativeFilesInJar

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

dependencies {
    compile project(":snobot_sim_utilities")
    
    compile 'edu.wpi.first.ntcore:ntcore-java:' + getNtCoreVersion()
    compile 'edu.wpi.first.cscore:cscore-java:' + getCsCoreVersion()
    
    testCompile 'junit:junit:4.12'
    testCompile 'edu.wpi.first.wpilibj:wpilibj-java:' + getWpilibjVersion()
    testRuntime 'edu.wpi.first.ntcore:ntcore-jni:' + getNtCoreVersion() + ':all'
    testRuntime 'edu.wpi.first.cscore:cscore-jni:' + getCsCoreVersion() + ':all'
    testRuntime 'org.opencv:opencv-java:' + getWpilibOpencvVersion()
    testRuntime 'org.opencv:opencv-jni:' + getWpilibOpencvVersion() + ':all'
    testRuntime combineNativeLibrariesIntoOne.outputs.files
    
    testCompile project(':ctre_override')
    testCompile "openrio.mirror.third.ctre:CTRE-phoenix-java:" + getCtreVersion()
    testCompile "openrio.mirror.third.kauailabs:navx-java:" + getNavxVersion()
    
}

def jniClasses = [
    'com.snobot.simulator.jni.JoystickJni',
    'com.snobot.simulator.jni.MotorConfigFactoryJni',
    'com.snobot.simulator.jni.RobotStateSingletonJni',
    'com.snobot.simulator.jni.SimulationConnectorJni',
    'com.snobot.simulator.jni.SnobotSimulatorJni',
    'com.snobot.simulator.jni.module_wrapper.AccelerometerWrapperJni',
    'com.snobot.simulator.jni.module_wrapper.AnalogSourceWrapperJni',
    'com.snobot.simulator.jni.module_wrapper.DigitalSourceWrapperJni',
    'com.snobot.simulator.jni.module_wrapper.EncoderWrapperJni',
    'com.snobot.simulator.jni.module_wrapper.GyroWrapperJni',
    'com.snobot.simulator.jni.module_wrapper.RelayWrapperJni',
    'com.snobot.simulator.jni.module_wrapper.SolenoidWrapperJni',
    'com.snobot.simulator.jni.module_wrapper.SpeedControllerWrapperJni'
]


model {
    jniConfigs {
        snobotSimJniShared(JNIConfig) {
            jniDefinitionClasses = jniClasses
            jniArmHeaderLocations = [ all: file("${projectDir}/src/arm-linux-jni") ]
            sourceSets = [ project.sourceSets.main ]
        }
        snobotSimJniStatic(JNIConfig) {
            jniDefinitionClasses = jniClasses
            jniArmHeaderLocations = [ all: file("${projectDir}/src/arm-linux-jni") ]
            sourceSets = [ project.sourceSets.main ]
        }
    }
    exportsConfigs {
        snobotSimJniShared(ExportsConfig) {
            x86SymbolFilter = { symbols->
                def retList = []
                return retList
            }
            x64SymbolFilter = { symbols->
                def retList = []
                return retList
            }
        }
    }
    dependencyConfigs {
        wpiutil(DependencyConfig) {
            groupId = 'edu.wpi.first.wpiutil'
            artifactId = 'wpiutil-cpp'
            headerClassifier = 'headers'
            ext = 'zip'
            version = '+'
            sharedConfigs = [ snobotSimJniShared: [] ]
        }
        halsim(DependencyConfig) {
            groupId = 'edu.wpi.first.hal'
            artifactId = 'hal'
            headerClassifier = 'headers'
            ext = 'zip'
            version = getHalHeadersVersion()
            sharedConfigs = [ snobotSimJniShared: [] ]
        }
    }
    components {
        snobotSimJniShared(NativeLibrarySpec) {
            baseName = 'snobotSimJni'
            sources {
                cpp {
                    source {
                        srcDirs = ['cpp']
                        includes = ['**/*.cpp']
                    }
                }
            }
        }

    }
    binaries {
        withType(NativeBinarySpec) {
        
            if(it.targetPlatform.architecture.name != "athena")
            {
                project(':snobot_sim').addSnobotSimToLinker(it)
            }
            else
            {
                buildable = false
            }
        }
        withType(StaticLibraryBinarySpec) {
            it.buildable = false
        }
    }
}

/////////////////////////////////////////////////////////////////////////////////
// Style/Formatting stuff
checkstyle {
    toolVersion = "8.1"
    configFile = new File(rootDir, "styleguide/checkstyle.xml")
}

pmd {
    consoleOutput  = true
    reportsDir = file("${project.buildDir}/reports/pmd")
    ruleSetFiles = files(file("$rootDir/styleguide/pmd-ruleset.xml"))
    ruleSets = []
}

findbugs {
    excludeFilter = file("$rootDir/styleguide/findbugs_suppressions.xml")
    effort = "max"
}
tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}
/////////////////////////////////////////////////////////////////////////////////

apply from: "publish.gradle"
